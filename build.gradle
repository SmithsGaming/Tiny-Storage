// Load the properties of this project.
ext.configFile = file "build.properties"

//Reference the properties inside the project:
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}


buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    mavenCentral()
	maven { 
        name 'OrionMavenRepo'
        url 'http://mavenrepo.orionminecraft.com/'
    }
	maven {
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
	maven{
		name "CB Repo"
		url = "http://chickenbones.net/maven/"
	}
}

apply plugin: 'forge'
apply plugin: 'curseforge'
apply plugin: 'maven'

//These configurations are needed for uploading to a MavenRepo:
configurations {
    deployerJars
}

version = config.mod_version.toString() + "_" + System.getenv().TRAVIS_BUILD_NUMBER.toString()
if(System.getenv().TRAVIS_BRANCH.toString().contains("Dev"))
{
    version = version + "-Dev"
}

group= "com.timthebrick.tinystorage" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "TinyStorage"

minecraft {
    version = config.forge_version.toString() + "-" + config.minecraft_version
    runDir = "eclipse"
	replace '@VERSION@', project.version
    replace '@MC_VERSION@', version
}

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-ftp:2.2'
	compile(config.waila_version.toString()) {
        exclude group: 'mcp.mobius.waila'
    }
	compile(config.chicken_lib_version.toString())
    compile(config.chicken_core_version.toString())
    compile(config.nei_version.toString())
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
	exclude '**/Thumbs.db'
}

jar {
        manifest {
        attributes 'FMLAT': 'TinyStorage_at.cfg'
    }
}

task devJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include '**'
    }

    extension = 'jar'
    classifier = 'dev'
}


artifacts {
    archives devJar
}

//The following function comes with thanks to AbrarSyed:
//Copying the coremod dependencies from the classpath to the mods directory so the system can load them in a Dev Env.
//This is not executed during build!
task copyChicken(type: Copy){
    from configurations.compile
    include "**/*Chicken*.jar", "**/*NotEnoughItems*.jar"
    exclude "**/CodeChickenLib*" // because CCC downloads it anyways.. -_-
    into file(minecraft.runDir + "/mods") // parallel to the assets dir
}

curse {
    dependsOn 'reobf'

    if (System.getenv().CURSEAPIKEY != null)
    {
        if (!config.build_mode.toString().trim().equals("SYNC"))
        {
            if ((config.build_mode.toString().trim().equals("RELEASE") && System.getenv().TRAVIS_BRANCH.toString().toLowerCase().contains("master")))
            {
                projectId = System.getenv().CURSEPROJECTID
                apiKey = System.getenv().CURSEAPIKEY
                changelog = "Check out: " + "https://github.com/" + System.getenv().TRAVIS_REPO_SLUG.toString() + "/commits/" + System.getenv().TRAVIS_BRANCH.toString() + " for all Changes."
                releaseType = "Release"
            }
            else
            {
                logger.lifecycle("Cannot run the CurseUpload sequence. Curse upload is only done on the Development-Branch when in Debug mode, or on any other branch in Release mode. Current Branch: " + System.getenv().TRAVIS_BRANCH.toString() + " - Current build mode: " + config.build_mode.toString())
            }
        }
        else
        {
            logger.lifecycle("Cannot run CurseUpload sequence. We are currently synchronising Computers.")
        }
    }
    else
    {
        logger.lifecycle("Cannot run the CurseUpload sequence. No API-Key was available.")
    }
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
            logger.lifecycle("Setting MavenRepo as publish")
            repository(url: "ftp://mavenrepo.orionminecraft.com") {
                authentication(userName: "mavenrepo.orionminecraft.com|" + System.getenv().FTPUserName.toString(), password: System.getenv().FTPPassword.toString())
			}
        pom {
			groupId = project.group
            version = config.minecraft_version + "_" + project.version
            artifactId = project.archivesBaseName
        }

        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'Tiny Storage'
            url 'https://github.com/Tim020/Tiny-Storage'

            scm {
                url 'https://github.com/Tim020/Tiny-Storage'
                connection 'scm:git:git://github.com/Tim020/Tiny-Storage.git'
                developerConnection 'scm:git:git@github.com/Tim020/Tiny-Storage.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/Tim020/Tiny-Storage/issues'
            }

            developers {
                developer {
                    id 'TimtheBrick'
                    name 'Tim'
                    roles {
                        role 'developer'
                    }
                }
            }
        }
    }
}

//The external task that executes the uploadAtchives function.
task('uploadJars', dependsOn: uploadArchives) {
    description = "uploads JARs"
}
